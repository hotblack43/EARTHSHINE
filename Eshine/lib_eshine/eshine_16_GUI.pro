
;===============================================================================
;
; ESHINE SIMULATION GRAPHICAL USER INTERFACE
;
; Assemble the following information and run the simulation:
;
; - Julian date or phase angle + libration parameters.
; - Observer's location
; - Moon albedos and BRDFs
; - Earth albedos and BRDFs
; - Sky background
; - Camera & instrument
; - Filter properties
; - CCD effects
; - Run mode
; - Output files
;
; Version 16
;
; Authors: Hans Gleisner & Peter Thejll   (c) Danish Meteorological Institute
;
;===============================================================================


PRO eshine_16_GUI_event, event

  WIDGET_CONTROL, event.TOP, GET_UVALUE=widgetID

  ; retrieve all widget IDs
  btngrpGeom    = widgetID[0]
  txtJDyr       = widgetID[1]
  txtJDmon      = widgetID[2]
  txtJDday      = widgetID[3]
  txtJDhr       = widgetID[4]
  txtJDmnt      = widgetID[5]
  txtJDsec      = widgetID[6]
  txtPhaseAngle = widgetID[7]
  txtLatLib     = widgetID[8]
  txtLonLib     = widgetID[9]
  txtPALib      = widgetID[10]
  listObs       = widgetID[11]
  lblXobs       = widgetID[12]
  lblYobs       = widgetID[13]
  lblZobs       = widgetID[14]
  lblUnitsXobs  = widgetID[15]
  lblUnitsYobs  = widgetID[16]
  lblUnitsZobs  = widgetID[17]
  txtXobs       = widgetID[18]
  txtYobs       = widgetID[19]
  txtZobs       = widgetID[20]
  listMoonAlb   = widgetID[21]
  listMoonBRDF  = widgetID[22]
  listEarthAlb  = widgetID[23]
  listEarthBRDF = widgetID[24]
  btngrpSky     = widgetID[25]
  txtNcols      = widgetID[26]
  txtNrows      = widgetID[27]
  txtPixScl     = widgetID[28]
  btngrpFilter  = widgetID[29]
  btngrpCCDeff  = widgetID[30]
  btngrpSimOpt  = widgetID[31]
  btngrpAction  = widgetID[32]
  btngrpImgFile = widgetID[33]
  btngrpFileFmt = widgetID[34]
  txtSeqStep    = widgetID[35]
  txtSeqLength  = widgetID[36]
  btnRun        = widgetID[37]
  btnQuit       = widgetID[38]

  ; if the event was generated by the observatory drop list, set new widget VALUEs for the Xobs, Yobs, and Zobs labels
  if (event.ID EQ listObs) then begin
    if WIDGET_INFO(listObs,/droplist_select) EQ WIDGET_INFO(listObs,/droplist_number)-1 then begin
      WIDGET_CONTROL, lblXobs,       SET_VALUE='X'
      WIDGET_CONTROL, lblYobs,       SET_VALUE='Y'
      WIDGET_CONTROL, lblZobs,       SET_VALUE='Z'
      WIDGET_CONTROL, lblUnitsXobs,  SET_VALUE='km'
      WIDGET_CONTROL, lblUnitsYobs,  SET_VALUE='km'
      WIDGET_CONTROL, lblUnitsZobs,  SET_VALUE='km'
    endif else begin
      WIDGET_CONTROL, lblXobs,       SET_VALUE='lat'
      WIDGET_CONTROL, lblYobs,       SET_VALUE='lon'
      WIDGET_CONTROL, lblZobs,       SET_VALUE='alt'
      WIDGET_CONTROL, lblUnitsXobs,  SET_VALUE='deg N'
      WIDGET_CONTROL, lblUnitsYobs,  SET_VALUE='deg W'
      WIDGET_CONTROL, lblUnitsZobs,  SET_VALUE='m'
    endelse
  endif

  ; if the event was generated by an action button, get the action from the widget UVALUE
  action = 'no'
  if (event.ID EQ btnRun) OR (event.ID EQ btnQuit) then begin
    WIDGET_CONTROL, event.ID,  GET_UVALUE=action
  endif

  ; retrieve widget VALUEs for textboxes and droplists.
  ; convert from string arrays to string scalars
  WIDGET_CONTROL, btngrpGeom,    GET_VALUE=geom_input
  WIDGET_CONTROL, txtJDyr,       GET_VALUE=yrstrng
  WIDGET_CONTROL, txtJDmon,      GET_VALUE=monstrng
  WIDGET_CONTROL, txtJDday,      GET_VALUE=daystrng
  WIDGET_CONTROL, txtJDhr,       GET_VALUE=hrstrng
  WIDGET_CONTROL, txtJDmnt,      GET_VALUE=mntstrng
  WIDGET_CONTROL, txtJDsec,      GET_VALUE=secstrng
  WIDGET_CONTROL, txtPhaseAngle, GET_VALUE=phasestrng
  WIDGET_CONTROL, txtLatLib,     GET_VALUE=latlibstrng
  WIDGET_CONTROL, txtLonLib,     GET_VALUE=lonlibstrng
  WIDGET_CONTROL, txtPALib,      GET_VALUE=philibstrng
  WIDGET_CONTROL, listObs,       GET_VALUE=Obs_list
  WIDGET_CONTROL, txtXobs,       GET_VALUE=Xobs_strng
  WIDGET_CONTROL, txtYobs,       GET_VALUE=Yobs_strng
  WIDGET_CONTROL, txtZobs,       GET_VALUE=Zobs_strng
  WIDGET_CONTROL, listMoonAlb,   GET_VALUE=MoonAlb_list
  WIDGET_CONTROL, listMoonBRDF,  GET_VALUE=MoonBRDF_list
  WIDGET_CONTROL, listEarthAlb,  GET_VALUE=EarthAlb_list
  WIDGET_CONTROL, listEarthBRDF, GET_VALUE=EarthBRDF_list
  WIDGET_CONTROL, btngrpSky,     GET_VALUE=sky_input
  WIDGET_CONTROL, txtNcols,      GET_VALUE=Ncols_strng
  WIDGET_CONTROL, txtNrows,      GET_VALUE=Nrows_strng
  WIDGET_CONTROL, txtPixScl,     GET_VALUE=PixScl_strng
  WIDGET_CONTROL, btngrpFilter,  GET_VALUE=filter_input
  WIDGET_CONTROL, btngrpCCDeff,  GET_VALUE=CCDeff_input
  WIDGET_CONTROL, btngrpSimOpt,  GET_VALUE=simopt_input
  WIDGET_CONTROL, btngrpAction,  GET_VALUE=action_input
  WIDGET_CONTROL, btngrpImgFile, GET_VALUE=imgfile_input
  WIDGET_CONTROL, btngrpFileFmt, GET_VALUE=filefmt_input
  WIDGET_CONTROL, txtSeqLength,  GET_VALUE=SeqLength_strng
  WIDGET_CONTROL, txtSeqStep,    GET_VALUE=SeqStep_strng
  yrstrng  = yrstrng[0]
  monstrng = monstrng[0]
  daystrng = daystrng[0]
  hrstrng  = hrstrng[0]
  mntstrng = mntstrng[0]
  secstrng = secstrng[0]
  phasestrng      = phasestrng[0]
  latlibstrng     = latlibstrng[0]
  lonlibstrng     = lonlibstrng[0]
  philibstrng     = philibstrng[0]
  Xobs_strng      = Xobs_strng[0]
  Yobs_strng      = Yobs_strng[0]
  Zobs_strng      = Zobs_strng[0]
  Ncols_strng     = Ncols_strng[0]
  Nrows_strng     = Nrows_strng[0]
  PixScl_strng    = PixScl_strng[0]
  SeqLength_strng = SeqLength_strng[0]
  SeqStep_strng   = SeqStep_strng[0]

  if (geom_input EQ 0) then begin
    yr  = fix(yrstrng)
    mon = fix(monstrng)
    day = fix(daystrng)
    hr  = fix(hrstrng)
    mnt = fix(mntstrng)
    sec = float(secstrng)
    JD  = double(julday(mon,day,yr,hr,mnt,sec))
    phase_angle = 0.0
    lat_lib = 0.0
    lon_lib = 0.0
    phi_lib = 0.0
  endif else if (geom_input EQ 1) then begin
    JD = double(-1.0)
    phase_angle = float(phasestrng)
    lat_lib = float(latlibstrng)
    lon_lib = float(lonlibstrng)
    phi_lib = float(philibstrng)
  endif

  Obs_strng = Obs_list[WIDGET_INFO(listObs, /droplist_select)]
  if strcmp(Obs_strng,'MEEQ  X,Y,Z',/fold_case) then begin
    olat = 384400.0
    olon = 0.0
    oalt = 0.0
    olat_strng = string(round(olat),FORMAT='(I6)')
    olon_strng = string(round(olon),FORMAT='(I6)')
    oalt_strng = string(round(oalt),FORMAT='(I6)')
  endif else if strcmp(Obs_strng,'Earth''s center',/fold_case) then begin
    olat = 0.0
    olon = 0.0
    oalt = -6365000.0
    olat_strng = string(olat,FORMAT='(F6.2)')
    olon_strng = string(olon,FORMAT='(F6.2)')
    oalt_strng = string(round(oalt),FORMAT='(I8)')
  endif else if strcmp(Obs_strng,'DMI',/fold_case) then begin
    olat = 55.6
    olon = 347.3
    oalt = 15.0
    olat_strng = string(olat,FORMAT='(F6.2)')
    olon_strng = string(olon,FORMAT='(F6.2)')
    oalt_strng = string(round(oalt),FORMAT='(I6)')
  endif else begin
    observatory, Obs_strng, obsdata
    olat = obsdata.latitude
    olon = obsdata.longitude
    oalt = obsdata.altitude
    olat_strng = string(olat,FORMAT='(F6.2)')
    olon_strng = string(olon,FORMAT='(F6.2)')
    oalt_strng = string(round(oalt),FORMAT='(I6)')
  endelse
  WIDGET_CONTROL, txtXobs, SET_VALUE=olat_strng
  WIDGET_CONTROL, txtYobs, SET_VALUE=olon_strng
  WIDGET_CONTROL, txtZobs, SET_VALUE=oalt_strng


  if strcmp(action,'quit',/fold) then begin

    WDELETE, 0
    WDELETE, 1
    WIDGET_CONTROL, event.TOP, /DESTROY

  endif else if strcmp(action,'run',/fold) then begin

    if strcmp(Obs_strng,'MEEQ  X,Y,Z',/fold_case) then begin
      obsname = Obs_strng
      obssys = 'MEEQ'
      Xobs = olat      ;
      Yobs = olon      ; observer's location in MEEQ coordinates [km]
      Zobs = oalt      ;
    endif else begin
      obsname = Obs_strng
      obssys = 'GEO'
      Xobs = (6365.0+oalt/1000.0)*cos(olat*!DPI/180.0D)*cos(olon*!DPI/180.0D)
      Yobs = (6365.0+oalt/1000.0)*cos(olat*!DPI/180.0D)*sin(olon*!DPI/180.0D)
      Zobs = (6365.0+oalt/1000.0)*sin(olat*!DPI/180.0D)
    endelse

    ;-------------------------------------------------------------------
    ; Earth and Moon reflectance specifications
    ;-------------------------------------------------------------------
    moon_albedo  = fix(WIDGET_INFO(listMoonAlb, /droplist_select))      ; 0=0.0720,  1=HIRES 750
    moon_BRDF    = fix(WIDGET_INFO(listMoonBRDF,/droplist_select))      ; 0=Lambert, 1=Hapke -63
    earth_albedo = fix(WIDGET_INFO(listEarthAlb, /droplist_select))     ; 0=0.3000,  1=cloud-free Earth
    earth_BRDF   = fix(WIDGET_INFO(listEarthBRDF,/droplist_select))     ; 0=Lambert
    ;
     datalib='/home/pth/SCIENCEPROJECTS/moon/Eshine/data_eshine'
    ; datalib = 'C:/EarthShine/Simulations/data_eshine'

    ;-------------------------------------------------------------------
    ; Sky background
    ;-------------------------------------------------------------------
    if_sky = 0
    if (sky_input[0] EQ 1) then if_sky = 1
    skylevel = 10.0

    ;-------------------------------------------------------------------
    ; Camera & CCD
    ;-------------------------------------------------------------------
    ; camera_ID = 'SXVH9'
    camera_ID = 'STL1001E'
    ;
    CCDcols = fix(Ncols_strng)        ; image size in terms of pixels
    CCDrows = fix(Nrows_strng)        ; image size in terms of pixels
    pixelscale = float(PixScl_strng)  ; arc seconds per pixel

    ;-------------------------------------------------------------------
    ; Filters
    ;-------------------------------------------------------------------
    if_filter = 0
    if (filter_input[0] EQ 1) then if_filter = 1
    filterfact = 10000

    ;-------------------------------------------------------------------
    ; CCD effects
    ;-------------------------------------------------------------------
    if_CCDeffects = 0
    if (CCDeff_input[0] EQ 1) then if_CCDeffects = 1
    exptime  = 0.15

    ;-------------------------------------------------------------------
    ; Simulation options
    ;-------------------------------------------------------------------
    if_librate            = 1
    if_variable_distances = 1
    if (simopt_input[0] EQ 1) then if_librate = 0
    if (simopt_input[1] EQ 1) then if_variable_distances = 0

    ;-------------------------------------------------------------------
    ; Set the graphics device to 'WIN', 'X', or 'PS'
    ;-------------------------------------------------------------------
    if_show = 1
    device_str='WIN'
    ; device_str='X'
    ; device_str='PS'

    if action_input EQ 0 then begin
    ; 0=single image

      ipict = -1
      iframe = -1
      if (total(filefmt_input) GT 0.0) then begin
        ipict = 1
      endif

      eshine_core, JD, phase_angle, lat_lib, lon_lib, phi_lib, $
                   obsname, obssys, Xobs, Yobs, Zobs, $
                   moon_albedo, moon_BRDF, earth_albedo, earth_BRDF, datalib, $
                   camera_ID, CCDcols, CCDrows, pixelscale, $
                   if_filter, filterfact, $
                   if_sky, skylevel, $
                   if_CCDeffects, exptime, $
                   if_librate, if_variable_distances, $
                   image_I, image_16bit, image_info

      add_telescope_and_filter_effects, image_16bit, if_filter, image_info.phase_angle_E, CCDcols, $
                                        filterfact, skylevel, if_sky

      if (if_CCDeffects eq 1) then begin
        generate_CCD, image_16bit, iframe, camera_ID, exptime, CCD_out, raw_CCD
        image_16bit = uint(CCD_out)
      endif else begin
        image_16bit = uint(image_16bit)
      endelse

      if (imgfile_input[0]) then  eshine_imagetofile, image_16bit, image_info, filefmt_input, ipict, iframe, mpegID
      if (imgfile_input[1]) then  eshine_imagetofile, image_I, image_info, filefmt_input, ipict, iframe, mpegID
      if (if_show) then           eshine_imagetoscreen, image_16bit, image_info, if_show, device_str

    endif else if (action_input EQ 1) AND (JD LT 0.0) then begin
    ; 1=image sequence

      dialogText = DIALOG_MESSAGE( 'You must give a start time for the image sequence.', /INFORMATION)

    endif else if (action_input EQ 1) AND (JD GT 0.0) then begin
    ; 1=image sequence

      ipict = -1
      iframe = -1
      if (filefmt_input[0] EQ 1 OR filefmt_input[1] EQ 1) then begin
        ipict = 0
      endif
      if (filefmt_input[2] EQ 1) then begin
        iframe = 0
        mpegID = MPEG_OPEN([512,512],filename='LunarSeq.mpg',quality=80)
      endif

      JDstart = JD
      JDend   = JDstart + float(SeqLength_strng)
      JDstep  = float(SeqStep_strng)/24.0

      for iJD=JDstart,JDend,JDstep do begin

        print, ' '
        print,'iframe = ', iframe
        print,'iJD    = ', iJD

        eshine_core, iJD, phase_angle, lat_lib, lon_lib, phi_lib, $
                     obsname, obssys, Xobs, Yobs, Zobs, $
                     moon_albedo, moon_BRDF, earth_albedo, earth_BRDF, datalib, $
                     camera_ID, CCDcols, CCDrows, pixelscale, $
                     if_filter, filterfact, $
                     if_sky, skylevel, $
                     if_CCDeffects, exptime, $
                     if_librate, if_variable_distances, $
                     image_I, image_16bit, image_info

        add_telescope_and_filter_effects, image_16bit, if_filter, image_info.phase_angle_E, CCDcols, $
                                          filterfact, skylevel, if_sky

        if (if_CCDeffects eq 1) then begin
          generate_CCD, image_16bit, iframe, camera_ID, exptime, CCD_out, raw_CCD
          image_16bit = uint(CCD_out)
        endif else begin
          image_16bit = uint(image_16bit)
        endelse

        if (imgfile_input[0] EQ 1) then  eshine_imagetofile, image_16bit, image_info, filefmt_input, ipict, iframe, mpegID
        if (imgfile_input[1] EQ 1) then  eshine_imagetofile, image_I, image_info, filefmt_input, ipict, iframe, mpegID
        if (if_show) then                eshine_imagetoscreen, image_16bit, image_info, if_show, device_str

        if (iframe EQ 0) then begin
          seq_info = REPLICATE(image_info,ceil((JDend-JDstart)/JDstep)+1)
        endif
        seq_info[iframe] = image_info

        ipict = ipict + 1
        iframe = iframe + 1

      endfor

      if (iframe GE 0) then begin
        MPEG_SAVE, mpegID, FILENAME='LunarSeq.mpg'
        MPEG_CLOSE, mpegID
      endif

    endif

  endif


END





PRO eshine_16_GUI

  base = WIDGET_BASE(title='DMI EarthShine Simulator v16',xoffset=5,yoffset=5,xsize=740,ysize=520)

  ; EARTH-MOON-SUN GEOMETRY
  headerG1  = WIDGET_LABEL(base,value='SUN - EARTH - MOON GEOMETRY',font='arial*bold*16',xoffset=20,yoffset=30)
  btngrpGeom = CW_BGROUP(base,['ephemerides:','phase && lib:'],font='arial*16',set_value=1,row=2,/exclusive,xoffset=15,xsize=120,yoffset=65,ysize=60)
  ; from date and time
  lblJDyr  = WIDGET_LABEL(base,value='yr',font='arial*14',xoffset=164,yoffset=55)
  lblJDmon = WIDGET_LABEL(base,value='mon',font='arial*14',xoffset=191,yoffset=55)
  lblJDday = WIDGET_LABEL(base,value='day',font='arial*14',xoffset=215,yoffset=55)
  lblJDhr  = WIDGET_LABEL(base,value='hr',font='arial*14',xoffset=240,yoffset=55)
  lblJDmnt = WIDGET_LABEL(base,value='min',font='arial*14',xoffset=259,yoffset=55)
  lblJDsec = WIDGET_LABEL(base,value='sec',font='arial*14',xoffset=282,yoffset=55)
  txtJDyr  = WIDGET_TEXT(base,/editable,value='2006',xoffset=157,yoffset=70,xsize=4,ysize=1)
  txtJDmon = WIDGET_TEXT(base,/editable,value=' 7',  xoffset=191,yoffset=70,xsize=2,ysize=1)
  txtJDday = WIDGET_TEXT(base,/editable,value='31',  xoffset=213,yoffset=70,xsize=2,ysize=1)
  txtJDhr  = WIDGET_TEXT(base,/editable,value='9',   xoffset=235,yoffset=70,xsize=2,ysize=1)
  txtJDmnt = WIDGET_TEXT(base,/editable,value=' 0',  xoffset=257,yoffset=70,xsize=2,ysize=1)
  txtJDsec = WIDGET_TEXT(base,/editable,value=' 0',  xoffset=279,yoffset=70,xsize=2,ysize=1)
  ; specified by user
  lblPhaseAngle = WIDGET_LABEL(base,value='phase angle',xoffset=147,yoffset=100)
  lblLatLib = WIDGET_LABEL(base,value='lat',xoffset=236,yoffset=100)
  lblLonLib = WIDGET_LABEL(base,value='lon',xoffset=259,yoffset=100)
  lblPALib = WIDGET_LABEL(base,value='PA    (libration)',xoffset=282,yoffset=100)
  txtPhaseAngle = WIDGET_TEXT(base,/editable,value='90',xoffset=159,yoffset=115,xsize=3,ysize=1)
  txtLatLib = WIDGET_TEXT(base,/editable,value='0',xoffset=233,yoffset=115,xsize=2,ysize=1)
  txtLonLib = WIDGET_TEXT(base,/editable,value='0',xoffset=256,yoffset=115,xsize=2,ysize=1)
  txtPALib = WIDGET_TEXT(base,/editable,value='0',xoffset=279,yoffset=115,xsize=2,ysize=1)

  ; observer
  headerO1 = WIDGET_LABEL(base,value='OBSERVATORY',font='arial*bold*16',xoffset=20,yoffset=180)
  labelO1  = WIDGET_LABEL(base,value='location',font='arial*16',xoffset=20,yoffset=208)
  listObs = WIDGET_DROPLIST(base,value=['DMI','LaPalma','MSO','SAAO','CFHT','ESO','Earth''s center','MEEQ  X,Y,Z'],uvalue=0,xsize=100,xoffset=70,yoffset=206)
  lblXobs = WIDGET_LABEL(base,value='lat',font='arial*14',/align_center,xoffset=215,yoffset=175)
  lblYobs = WIDGET_LABEL(base,value='lon',font='arial*14',/align_center,xoffset=260,yoffset=175)
  lblZobs = WIDGET_LABEL(base,value='alt',font='arial*14',/align_center,xoffset=306,yoffset=175)
  lblUnitsXobs = WIDGET_LABEL(base,value='[deg N]',font='arial*14',/align_center,xoffset=205,yoffset=189)
  lblUnitsYobs = WIDGET_LABEL(base,value='[deg W]',font='arial*14',/align_center,xoffset=250,yoffset=189)
  lblUnitsZobs = WIDGET_LABEL(base,value='  [m]  ',font='arial*14',/align_center,xoffset=300,yoffset=189)
  txtXobs = WIDGET_TEXT(base,value=' 55.60',xoffset=200,yoffset=205,xsize=6)
  txtYobs = WIDGET_TEXT(base,value='347.30',xoffset=246,yoffset=205,xsize=6)
  txtZobs = WIDGET_TEXT(base,value='    15',xoffset=292,yoffset=205,xsize=6)

  ; Moon's reflectance properties
  headerM1 = WIDGET_LABEL(base,value='MOON MODEL',font='arial*bold*16',xoffset=20,yoffset=270)
  labelM1  = WIDGET_LABEL(base,value='albedos',font='arial*16',xoffset=20,yoffset=298)
  labelM2  = WIDGET_LABEL(base,value='BRDF',font='arial*16',xoffset=20,yoffset=325)
  listMoonAlb = WIDGET_DROPLIST(base,value=['0.072 (uniform)','HIRES 750'],uvalue=1,xsize=100,xoffset=70,yoffset=295)
  listMoonBRDF = WIDGET_DROPLIST(base,value=['Lambert','Hapke -63'],uvalue=1,xsize=100,xoffset=70,yoffset=322)
  WIDGET_CONTROL, listMoonAlb, set_droplist_select=1
  WIDGET_CONTROL, listMoonBRDF, set_droplist_select=1

  ; Earth's reflectance properties
  headerE1 = WIDGET_LABEL(base,value='EARTH MODEL',font='arial*bold*16',xoffset=200,yoffset=270)
  labelE1  = WIDGET_LABEL(base,value='albedos',font='arial*16',xoffset=200,yoffset=298)
  labelE2  = WIDGET_LABEL(base,value='BRDF',font='arial*16',xoffset=200,yoffset=325)
  listEarthAlb = WIDGET_DROPLIST(base,value=['0.300 (uniform)','Cloud-free Earth'],uvalue=0,xsize=100,xoffset=250,yoffset=295)
  listEarthBRDF = WIDGET_DROPLIST(base,value=['Lambert (uniform)'],uvalue=0,xsize=100,xoffset=250,yoffset=322)
  WIDGET_CONTROL, listEarthAlb, set_droplist_select=0
  WIDGET_CONTROL, listEarthBRDF, set_droplist_select=0

  ; Sky background
  headerSky = WIDGET_LABEL(base,value='SKY BACKGROUND',font='arial*bold*16',xoffset=20,yoffset=385)
  btngrpSky = CW_BGROUP(base,['Sky'],font='arial*16',set_value=0,row=1,/nonexclusive,xoffset=25,xsize=80,yoffset=403,ysize=30)

  ; Create a draw widget as a separator.
  drawSeparator = WIDGET_DRAW(base, xoffset=370, yoffset=25, xsize=1, ysize=400)

  ; CCD camera
  headerCCD = WIDGET_LABEL(base,value='CCD CAMERA',font='arial*bold*16',xoffset=390,yoffset=30)
  txtNcols  = WIDGET_TEXT(base,/editable,value='1025',xoffset=391,yoffset=55,xsize=4)
  lblX      = WIDGET_LABEL(base,value='x',font='arial*bold*16',xoffset=429,yoffset=55)
  txtNrows  = WIDGET_TEXT(base,/editable,value='1025',xoffset=440,yoffset=55,xsize=4)
  lblPixelUnit = WIDGET_LABEL(base,value='[pixels]',xoffset=480,yoffset=60)
  txtPixScl = WIDGET_TEXT(base,/editable,value='2.50',xoffset=560,yoffset=55,xsize=4)
  lblPixSclUnit = WIDGET_LABEL(base,value='[arcsec/pixel]',xoffset=600,yoffset=60)

  ; Filters & CCD effects
  headerE1  = WIDGET_LABEL(base,value='FILTERS & CCD EFFECTS',font='arial*bold*16',xoffset=390,yoffset=105)
  btngrpFilter = CW_BGROUP(base,['half filter'],font='arial*16',set_value=0,row=1,/nonexclusive,xoffset=385,xsize=80,yoffset=128,ysize=30)
  btngrpCCDeff = CW_BGROUP(base,['CCD effects'],font='arial*16',set_value=0,row=1,/nonexclusive,xoffset=470,xsize=200,yoffset=128,ysize=30)

  ; Simulation options
  headerSimOpt = WIDGET_LABEL(base,value='SIMULATION OPTIONS',font='arial*bold*16',xoffset=390,yoffset=180)
  btngrpSimOpt = CW_BGROUP(base,['no libration','constant distances'],font='arial*16',set_value=[0,0],row=1,/nonexclusive,xoffset=385,xsize=250,yoffset=198,ysize=30)

  ; Create a draw widget as a separator.
  drawSeparator = WIDGET_DRAW(base, xoffset=390, yoffset=250, xsize=330, ysize=1)

  ; Run mode
  headerRunMode = WIDGET_LABEL(base,value='RUN MODE',font='arial*bold*16',xoffset=390,yoffset=270)
  btngrpAction  = CW_BGROUP(base,['image, single','image sequence'],font='arial*16',set_value=0,row=2,/exclusive,xoffset=385,xsize=120,yoffset=290,ysize=60)
  lblSeqStep    = WIDGET_LABEL(base,value='step',xoffset=410,yoffset=360)
  lblSeqLength  = WIDGET_LABEL(base,value='length',xoffset=410,yoffset=385)
  lblSeqStepUnit   = WIDGET_LABEL(base,value='[hr]',xoffset=475,yoffset=360)
  lblSeqLengthUnit = WIDGET_LABEL(base,value='[days]',xoffset=475,yoffset=385)
  txtSeqStep    = WIDGET_TEXT(base,/editable,value='   ',xoffset=443,yoffset=355,xsize=3)
  txtSeqLength  = WIDGET_TEXT(base,/editable,value='   ',xoffset=443,yoffset=380,xsize=3)

  ; Create a draw widget as a separator.
  drawSeparator = WIDGET_DRAW(base, xoffset=530, yoffset=270, xsize=1, ysize=155)

  ; Output files
  headerOutput = WIDGET_LABEL(base,value='OUTPUT FILES',font='arial*bold*16',xoffset=545,yoffset=270)
  btngrpImgFile = CW_BGROUP(base,['CCD image','rad. image'],font='arial*16',set_value=[1,1],row=1,/nonexclusive,xoffset=540,xsize=200,yoffset=300,ysize=30)
  btngrpFileFmt = CW_BGROUP(base,['FITS','TIFF','JPEG'],font='arial*16',set_value=[1,0,1],row=1,/nonexclusive,xoffset=540,xsize=200,yoffset=330,ysize=30)

  ; Start & Quit buttons
  btnRun  = WIDGET_BUTTON(base,value='Run',uvalue='run',font='arial*bold*italic*18',xsize=80,ysize=40,xoffset=420,yoffset=450)
  btnQuit = WIDGET_BUTTON(base,value='Quit',uvalue='quit',font='arial*bold*italic*18',xsize=80,ysize=40,xoffset=560,yoffset=450)

  ; Copyright
  labelCopyright = WIDGET_LABEL(base,value='(c) Danish Meteorological Institute',font='arial*14',xoffset=5,yoffset=505)

  WIDGET_CONTROL,base,SET_UVALUE=[btngrpGeom,txtJDyr,txtJDmon,txtJDday,txtJDhr,txtJDmnt,txtJDsec,txtPhaseAngle,txtLatLib,txtLonLib,txtPALib,   $
                                  listObs,lblXobs,lblYobs,lblZobs,lblUnitsXobs,lblUnitsYobs,lblUnitsZobs,txtXobs,txtYobs,txtZobs,listMoonAlb,  $
                                  listMoonBRDF,listEarthAlb,listEarthBRDF,btngrpSky,txtNcols,txtNrows,txtPixScl,btngrpFilter,btngrpCCDeff,     $
                                  btngrpSimOpt,btngrpAction,btngrpImgFile,btngrpFileFmt,txtSeqStep,txtSeqLength,btnRun,btnQuit]
  WIDGET_CONTROL,base,/REALIZE
  XMANAGER, 'eshine_16_GUI',base

END
